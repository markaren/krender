plugins {
    id 'base'
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0' apply(false)
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.6.1'
}

group 'info.laht.krender'
version '0.1'

println "Gradle version is ${gradle.getGradleVersion()}"

subprojects {
    
    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
        maven { url "https://dl.bintray.com/laht/mvn" }
        maven { url "https://dl.bintray.com/jmonkeyengine/org.jmonkeyengine" }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"

        def slf4j_version = '1.7.30'
        implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version
        runtimeOnly group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4j_version

        def junit_version = '5.6.2'
        testImplementation("org.junit.jupiter:junit-jupiter-api:$junit_version")
        testImplementation("org.junit.jupiter:junit-jupiter-params:$junit_version")
        testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junit_version")
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            javaParameters = true
        }
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    test {
        failFast = true
        useJUnitPlatform()
    }

}
